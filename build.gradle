buildscript {
    ext.kotlin_version = "1.3.72"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.30.0"
    ext.grpc_kotlin_version = "0.1.3"
    ext.aws_sdk_version = "2.13.42"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "com.google.protobuf" version "0.8.12"
}

group 'app.fourdrin'
version '1.0-SNAPSHOT'

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'

    // gRPC and Protobuf
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

    // Java 9+ dep
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    // AWS SDKs
    implementation "software.amazon.awssdk:s3:$aws_sdk_version"

    // Third Party
    implementation 'org.apache.curator:curator-framework:5.0.0'
    implementation 'org.apache.curator:curator-client:5.0.0'
    implementation 'org.apache.curator:curator-recipes:5.0.0'
    implementation 'com.google.code.gson:gson'
}

idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main")
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc { }
                grpckt { }
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

application {
    mainClassName = "app.fourdrin.sedai.SedaiKt"
}

